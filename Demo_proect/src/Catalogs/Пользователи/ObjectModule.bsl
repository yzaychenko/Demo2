#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Заполняет набор прав по пользователю ИБ.
// 
// Параметры:
//  ПользовательИБ - ПользовательИнформационнойБазы
Процедура ЗаполнитьНаборПрав(ПользовательИБ) Экспорт
	РолиИБ = Метаданные.Роли;
	НаборПрав.Очистить();
	Для Каждого СтрокаРоли Из РолиИБ Цикл
		НоваяРоль = НаборПрав.Добавить();
		НоваяРоль.ИмяРоли = СтрокаРоли.Имя;
		НоваяРоль.ПредставлениеРоли = СтрокаРоли.Синоним;
		НоваяРоль.Активно = ПользовательИБ.Роли.Содержит(СтрокаРоли);
	КонецЦикла;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;	
	
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(Наименование);
	Если ПользовательИБ = Неопределено Тогда
		ПользовательИБОбъект 			= ПользователиИнформационнойБазы.СоздатьПользователя();
		ПользовательИБОбъект.Имя 		= Наименование;
		ПользовательИБОбъект.ПолноеИмя 	= ПолноеИмя;
		ПользовательИБ = ПользовательИБОбъект;	
	КонецЕсли;
		
	ПользовательИБ.Роли.Очистить();
	АктивныеСтрокиНаборовПрав = НаборПрав.НайтиСтроки(Новый Структура("Активно", Истина));
	Для Каждого СтрокаНабораПрав Из АктивныеСтрокиНаборовПрав Цикл
		ТекущаяРоль = Метаданные.Роли.Найти(СтрокаНабораПрав.ИмяРоли);
		ПользовательИБ.Роли.Добавить(ТекущаяРоль);
	КонецЦикла;
		
	ПользовательИБ.Записать();
КонецПроцедуры

#КонецЕсли
